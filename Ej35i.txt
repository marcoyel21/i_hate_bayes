model
{
#Likelihood
for (i in 1:n) {
for (j in 1:m[i]) {
	y[i,j] ~ dnorm(mu[i,j],tau[i])
	mu[i,j] <- alpha+beta[i]+gama[dia[i]]+delta[mes[i]]+ gade[dia[i],mes[i]]+ epsilon[ano[i]]+theta[puente[i]]+nu*x[i,j]
	}
	}

#Priors
alpha ~ dnorm(0,0.001)
nu ~ dnorm(0,0.001)
for (i in 1:n) {beta[i] ~ dnorm(0,100)}
for (i in 1:7) {gama[i] ~ dnorm(0,0.01)}
for (i in 1:12) {delta[i] ~ dnorm(0,0.01)}
# prior distributions for the rest interaction parameters
for (i in 1:7) { for (j in 1:12) {gade[i,j] ~ dnorm(0,0.01)}}
for (i in 1:3) {epsilon[i] ~ dnorm(0,0.01)}
for (i in 1:2) {theta[i] ~ dnorm(0,0.01)}
for (i in 1:n) {tau[i] ~ dgamma(0.01,0.01)}

#Estimability constraints
alpha.est <- alpha+mean(beta[])+mean(gama[])+mean(delta[])+mean(epsilon[])+mean(theta[]) + mean(gade[,])
for (i in 1:n) {beta.est[i] <- beta[i]-mean(beta[]) }
for (i in 1:7) {gama.est[i] <- gama[i]-mean(gama[])  + mean(gade[i,]) - mean(gade[,]) } 
for (i in 1:12) {delta.est[i] <- delta[i]-mean(delta[]) + mean(gade[,i]) -mean(gade[,]) }
for (i in 1:3) {epsilon.est[i] <- epsilon[i]-mean(epsilon[])}
for (i in 1:2) {theta.est[i] <- theta[i]-mean(theta[])}
for (i in 1:7) { for (j in 1:12) { gade.est[i,j] <- gade[i,j] - mean(gade[i,]) - mean(gade[,j])  + mean(gade[,])  }}

#Predictive
for (i in 1:n) {
for (j in 1:m[i]) {
	yf[i,j] ~ dnorm(mu[i,j],tau[i])
	}
	}
}